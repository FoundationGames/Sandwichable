plugins {
	id 'fabric-loom' version '0.10-SNAPSHOT'
	id 'maven-publish'
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

static String terraformers() {
	def url = "https://maven.terraformersmc.com/"
	def conn = new URL(url).openConnection()
	conn.connect()
	url = conn.getResponseCode() == 521 ? "https://raw.githubusercontent.com/TerraformersMC/Archive/main/releases" : url
	return url
}

repositories {
	maven { url = terraformers() }
	maven { url = "https://maven.ryanliptak.com/" }
	maven { url = "https://maven.shedaniel.me/" }
	maven { url = "https://api.modrinth.com/maven" }
}

// please stop making mod menu transitive
dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	modImplementation("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}") {
		exclude(group: "com.terraformersmc")
		exclude(group: "io.github.prospector")
	}
	modApi "com.terraformersmc:modmenu:${project.modmenu_version}"

	modApi("squeek.appleskin:appleskin-fabric:${project.appleskin_version}:api") {
		exclude(group: "com.terraformersmc")
		exclude(group: "io.github.prospector")
	}
	modApi "maven.modrinth:mealapi:${project.mealapi_version}"
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_cfg_version}") {
		exclude(group: "net.fabricmc.fabric-api")
		exclude(group: "com.terraformersmc")
		exclude(group: "io.github.prospector")
	}

	modRuntimeOnly("squeek.appleskin:appleskin-fabric:${project.appleskin_version}") {
		exclude(group: "com.terraformersmc")
		exclude(group: "io.github.prospector")
	}
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

jar {
	from "LICENSE"
}

publishing {
	publications {
		mavenJava(MavenPublication) {
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	repositories {
		mavenLocal()
	}
}
